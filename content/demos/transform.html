<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<title>transform - Coordinate transformations</title>
</head>
<body>
<h1>transform - Coordinate transformations</h1>

    This demo illustrates the <a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a> module.
<p/>
    It requires <a href="https://metacpan.org/pod/PDL::Graphics::Simple">PDL::Graphics::Simple</a> installed and makes use of the image of
    M51 kindly provided by the Hubble Heritage group at the
    Space Telescope Science Institute.

<hr/>
<a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a> objects embody coordinate transformations.
<pre>
    use <a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a>;
</pre>
set up a simple linear scale-and-shift relation
<pre>
    $t = t_linear( Scale=>[2,-1], Post=>[100,0]);
    print $t;
</pre>
<h4>Output</h4>
<pre>
Linear (linear): fwd ok; inv ok.
  Pre-add: 0
  Post-add: [100 0]
  Forward matrix:
  [
   [ 2  0]
   [ 0 -1]
  ]
  Inverse matrix:
  [
   [0.5   0]
   [  0  -1]
  ]
  
</pre>

<hr/>
The simplest way to use <a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a> is to transform a set of
vectors.  To do this you use the "apply" method.
Define a few 2-vectors:
<pre>
    $xy = pdl([[0,1],[1,2],[10,3]]);
    print "xy: ", $xy;
</pre>
Transform the 2-vectors:
<pre>
    print "Transformed: ", $xy->apply( $t );
</pre>
<h4>Output</h4>
<pre>
xy: 
[
 [ 0  1]
 [ 1  2]
 [10  3]
]
Transformed: 
[
 [100  -1]
 [102  -2]
 [120  -3]
]
</pre>

<hr/>
You can invert and compose transformations with 'x' and '!'.
<pre>
    $u = t_linear( Scale=>10 );     # A new transformation (simple x10 scale)
    $xy = pdl([[0,1],[10,3]]);      # Two 2-vectors
    print "xy:   ",  $xy;
    print "xy':  ",  $xy->apply( !$t     );     # Invert $t from earlier.
    print "xy'': ",  $xy->apply( $u x !$t );    # Hit the result with $u.
</pre>
<h4>Output</h4>
<pre>
xy:   
[
 [ 0  1]
 [10  3]
]
xy':  
[
 [-50  -1]
 [-45  -3]
]
xy'': 
[
 [-500  -10]
 [-450  -30]
]
</pre>

<hr/>
<a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a> is useful for data resampling, and that's perhaps
the best way to demonstrate it.  First, we do a little bit of prep work:
Read in an image ($m51file has been set up by this demo to
contain the location of the file).  Transform is designed to
work well with FITS images that contain WCS scientific coordinate
information, but works equally well in pixel space.
<pre>
    $m51 = rfits($<a href="https://metacpan.org/pod/PDL::Demos::Transform_demo::m51file,{hdrcpy=>1})">PDL::Demos::Transform_demo::m51file,{hdrcpy=>1})</a>;
</pre>
we use a floating-point version of the image in some of the demos
to highlight the interpolation schemes.  (Note that the FITS
header gets deep-copied automatically into the new variable).
<pre>
    $m51_fl = $m51->float;
</pre>
Define a nice, simple scale-by-3 transformation.
<pre>
    $ts = t_scale(3);
</pre>

<hr/>
Resampling with ->map and no FITS interpretation works in pixel space.
Create a plot window, and display the original image
<pre>
    $win = pgswin( size=>[8,6], multi=>[2,2] ) ;
    $win->plot(with=>'image', $m51, { Title=>"M51" });
</pre>
Grow m51 by a factor of 3; origin is at lower left
(the "pix" makes the resampling happen in pixel coordinate
space, ignoring the FITS header)
<pre>
    $win->plot(with=>'image', $m51->map($ts, {pix=>1}),
      { Title=>"M51 grown by 3 (pixel coords)" });
</pre>
Shrink m51 by a factor of 3; origin still at lower left.
(You can invert the transform with a leading '!'.)
<pre>
    $win->plot(with=>'image', $m51->map(!$ts, {pix=>1}),
      { Title=>"M51 shrunk by 3 (pixel coords)" });
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-1.png"/>

<hr/>
You can work in scientific space (or any other space) by
wrapping your main transformation with something that translates
between the coordinates you want to act in, and the coordinates
you have.  Here, "t_fits" translates between pixels in the data
and arcminutes in the image plane.
<pre>
    $win->plot(with=>'points', pdl([1]), {title=>''}); # blank, clears
    $win->plot(with=>'image', $m51, { Title=>"M51" });
</pre>
Scale in scientific coordinates.
Here's a way to scale in scientific coordinates:
wrap our transformation in FITS-header transforms to translate
the transformation into scientific space.
<pre>
    $win->plot(with=>'image', $m51->map(!$ts->wrap(t_fits($m51)), {pix=>1}),
      { Title=>"M51 shrunk by 3 (sci. coords)" });
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-2.png"/>

<hr/>
If you don't specify "pix=>1" then the resampler works in scientific
FITS coordinates (if the image has a FITS header):
Scale in scientific coordinates (origin at center of galaxy)
<pre>
  $win->plot(with=>'fits', $m51->map($ts, $m51->hdr), { Title=>"M51 3x" });
</pre>
Instead of setting up a coordinate transformation you can use the
implicit FITS header matching.  Just tweak the template header:
<pre>
  $tohdr = $m51->hdr_copy;
  $tohdr->{CDELT1} /= 3;  # Magnify 3x in horiz direction
  $tohdr->{CDELT2} /= 3;  # Magnify 3x in vert direction
</pre>
Resample to match the new FITS header
(Note that, although the image is scaled exactly the same as before,
this time the scientific coordinates have scaled too.)
<pre>
  $win->plot(with=>'fits', $m51->map(t_identity(), $tohdr),
    { Title=>"3x (FITS)" });
</pre>

<hr/>
The three main resampling methods are "sample", "linear", and "jacobian".
Sampling is fastest, linear interpolation is better.  Jacobian resampling
is slow but prevents aliasing under skew or reducing transformations.
<pre>
 $win->plot(with=>'fits', $m51, { Title=>"M51" });
 $win->plot(with=>'fits', $m51_fl->map( $ts, $m51_fl, { method=>"sample" } ),
		{ Title=>"M51 x3 (sampled)" });
 $win->plot(with=>'fits', $m51_fl->map($ts, $m51_fl, {method=>"linear"}),
		{ Title=>"M51 x3 (interp.)"});
 $win->plot(with=>'fits', $m51_fl->map($ts, $m51_fl, { method=>"jacobian" }),
 	        { Title=>"M51 x3 (jacob.)"});
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-3.png"/>

<hr/>
Linear transformations are only the beginning.  Here's an example
using a simple nonlinear transformation:  radial coordinate transformation.
Original image
<pre>
 $win->plot(with=>'fits', $m51, { Title=>"M51" });
</pre>
Radial structure in M51 (linear radial scale; origin at (0,0) by default)
<pre>
 $tu = t_radial( u=>'degree' );
 $win->plot(with=>'fits', $m51_fl->map($tu),
    { Title=>"M51 radial (linear)", J=>0 });
</pre>
Radial structure in M51 (conformal/logarithmic radial scale)
<pre>
 $tu_c = t_radial( r0=>0.1 );  # Y axis 0 is at 0.1 arcmin
 $win->plot(with=>'fits', $m51_fl->map($tu_c),
    { Title=>"M51 radial (conformal)", YRange=>[0,4] } );
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-4.png"/>

<hr/>
<p/>
Wrapping transformations allows you to work in a convenient
space for what you want to do.  Here, we can use a simple
skew matrix to find (and remove) logarithmic spiral structures in
the galaxy.  The "unspiraled" images shift the spiral arms into
approximate straight lines.
<pre>
    $sp = 3.14159;  # Skew by 3.14159
</pre>
Skew matrix
<pre>
    $t_skew = t_linear(pre => [$sp * 130, 0] , matrix => pdl([1,0],[-$sp,1]));
</pre>
When put into conformal radial space, the skew turns into 3.14159
radians per scale height.
<pre>
    $t_untwist = t_wrap($t_skew, $tu_c);
</pre>
Press enter to see the result of these transforms...

<hr/>
<p/>
Note that you can use ->map and ->unmap as either PDL methods
or transform methods; what to do is clear from context.
<pre>
$win->plot(with=>'points', pdl([1]), {title=>''}); # blank, clears
</pre>
Original image
<pre>
$win->plot(with=>'fits', $m51, { Title=>"M51" });
</pre>
Skewed
<pre>
$win->plot(with=>'fits', $m51_fl->map( $t_skew ),
    { Title => "M51 skewed by pi in spatial coords" } );
</pre>
Untwisted -- show that m51 has a half-twist per scale height
<pre>
$win->plot(with=>'fits', $m51_fl->map( $t_untwist ),
    { Title => "M51 unspiraled (pi / r_s)"} );
</pre>
Untwisted -- the jacobian method uses variable spatial filtering
to eliminate spatial artifacts, at significant computational cost
(This may take some time to complete).
<pre>
$win->plot(with=>'fits', $m51_fl->map( $t_untwist, {m=>"jacobian"}),
    { Title => "M51 unspiraled (pi / r_s; antialiased)" } );
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-5.png"/>

<hr/>
Native FITS interpretation makes it easy to view your data in
your preferred coordinate system.  Here we zoom in on a 0.2x0.2
arcmin region of M51, sampling it to 100x100 pixels resolution.
<pre>
$m51 = float $m51;
$data = $m51->match([100,100],{or=>[[-0.05,0.15],[-0.05,0.15]]});
$s = "M51 closeup ("; $ss=" coords)";
$ps = " (pixels)";
$win = pgswin( size=>[8,4], multi=>[2,1] ) ;
$win->plot(with=>'image', $data,  { title=>"${s}pixel${ss}",
  xlabel=>"X$ps", ylabel=>"Y$ps", crange=>[600,750] } );
$win->plot(with=>'fits', $data, { title=>"${s}sci.${ss}",
  crange=>[600,750] } );
</pre>
<h4>Output</h4>

<img src="images/demos/transform/output-6.png"/>

<hr/>
Now rotate the image 360 degrees in 10 degree increments.
The 'match' method resamples $data to the rotated scientific
coordinate system in $hdr.  The "pixel coordinates" window shows
the resampled data in their new pixel coordinate system.
The "sci. coordinates" window shows the data remaining fixed in
scientific space, even though the pixels that represent them are
moving and rotating.
<pre>
$hdr = $data->hdr_copy;
for ($rot=0; $rot<360; $rot += 10) {
  $hdr->{CROTA2} = $rot;
  $d = $m51->match($hdr);
  $win->plot(with=>'image', $d,  { title=>"${s}pixel${ss}",
    xlabel=>"X$ps", ylabel=>"Y$ps", crange=>[600,750] } );
  $win->plot(with=>'fits', $d, { title=>"${s}sci.${ss}",
    xrange=>[-0.05,0.15], yrange=>[-0.05,0.15], crange=>[600,750] } );
}
</pre>
<h4>Output</h4>

<img src="images/demos/transform/vid-1.gif"/>

<hr/>
You can do the same thing even with nonsquare coordinates.
Here, we resample the same region in scientific space into a
150x50 pixel array.
<pre>
$data = $m51->match([150,50],{or=>[[-0.05,0.15],[-0.05,0.15]]});
$hdr = $data->hdr_copy;
for ($rot=0; $rot<360; $rot += 5) {
  $hdr->{CROTA2} = $rot;
  $d = $m51->match($hdr,{or=>[[-0.05,0.15],[-0.05,0.15]]});
  $win->plot(with=>'image', $d,  { title=>"${s}pixel${ss}",
    xlabel=>"X$ps", ylabel=>"Y$ps", crange=>[600,750] } );
  $win->plot(with=>'fits', $d, { title=>"${s}sci.${ss}",
    xrange=>[-0.05,0.15], yrange=>[-0.05,0.15], crange=>[600,750] } );
}
</pre>
<h4>Output</h4>

<img src="images/demos/transform/vid-2.gif"/>

<hr/>
 This concludes the <a href="https://metacpan.org/pod/PDL::Transform">PDL::Transform</a> demo.
<p/>
 Be sure to check the documentation for <a href="https://metacpan.org/pod/PDL::Transform::Cartography,">PDL::Transform::Cartography,</a>
 which contains common perspective and mapping coordinate systems
 that are useful for work on the terrestrial and celestial spheres,
 as well as other planets &c.
</body>
</html>
